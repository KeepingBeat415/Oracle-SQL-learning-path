### Using Dynamic SQL

OPEN - FOR, FETCH

```sql
DECLARE
  TYPE emp_cur_type IS REF CURSOR;
  emp_cursor      emp_cur_type;
  emp_record      employees%rowtype;
BEGIN
  OPEN emp_cursor FOR 'SELECT * FROM employees WHERE job_id = ''IT_PROG''';
    FETCH emp_cursor INTO emp_record;
    dbms_output.put_line(emp_record.first_name||emp_record.last_name);
  CLOSE emp_cursor;
END;
/
-- bind variable
DECLARE
  TYPE emp_cur_type  IS REF CURSOR;
  emp_cursor      emp_cur_type;
  emp_record      employees%rowtype;
BEGIN
  OPEN emp_cursor FOR 'SELECT * FROM employees WHERE job_id = :job' USING 'IT_PROG';
    FETCH emp_cursor INTO emp_record;
    dbms_output.put_line(emp_record.first_name||emp_record.last_name);
  CLOSE emp_cursor;
END;
/

DECLARE
  TYPE emp_cur_type  IS REF CURSOR;
  emp_cursor      emp_cur_type;
  emp_record      employees%rowtype;
BEGIN
  OPEN emp_cursor FOR 'SELECT * FROM employees WHERE job_id = :job' USING 'IT_PROG';
  LOOP
    FETCH emp_cursor INTO emp_record;
    EXIT WHEN emp_cursor%notfound;
    dbms_output.put_line(emp_record.first_name||emp_record.last_name);
  END LOOP;
  CLOSE emp_cursor;
END;
/

DECLARE
  TYPE emp_cur_type  IS REF CURSOR;
  emp_cursor      emp_cur_type;
  emp_record      employees%rowtype;
  v_table_name    VARCHAR(20);
BEGIN
  v_table_name := 'employees';
  OPEN emp_cursor FOR 'SELECT * FROM '||v_table_name||' WHERE job_id = :job' USING 'IT_PROG';
  LOOP
    FETCH emp_cursor INTO emp_record;
    EXIT WHEN emp_cursor%notfound;
    dbms_output.put_line(emp_record.first_name||emp_record.last_name);
  END LOOP;
  CLOSE emp_cursor;
END;
```

Using the DMBS_SQL package

- the subprograms of the DBMS_SQL package:
  - open_cursor(): opens a new cursor and returns the cursor ID
  - parse(cursor_id, statement, edition): parses the dynamic SQL statement
    - for `edition`
      - 0 or V6: database V6
      - 1 or native: default
      - 2 or V&: database V7
      - foreign_syntax: non-oracle database syntax
  - execute(cursor_id): executes the statement and returns the number of rows processed
  - fetch_rows(cursor_id): retrieves the next row
  - close_cursor(cursor_id): closes the given cursor
  - define_column(cursor_id, position, column): adds the column to the column list
  - bind_variable(cursor_id, name, value): binds values to the query
  - column_value(cursor_id, position, value): retrieves the selected column value

```sql
CREATE TABLE employees_copy AS SELECT * FROM employees;

/
set serveroutput on;
DECLARE
    v_table_name VARCHAR2(20) := 'employees_copy';
    v_cursor_id PLS_INTEGER;
    v_affected_rows PLS_INTEGER;
BEGIN
    v_cursor_id := dbms_sql.open_cursor;
    dbms_sql.parse(v_cursor_id, 'update '||v_table_name||' set salary=salary+500',dbms_sql.NATIVE);
    v_affected_rows := dbms_sql.EXECUTE(v_cursor_id);
    dbms_output.put_line(v_affected_rows|| ' rows are updated by dbms_sql!');
    dbms_sql.close_cursor(v_cursor_id);
END;

/

select * from employees_copy;
/

-- bind variables
DECLARE
    v_table_name varchar2(20) := 'employees_copy';
    v_cursor_id pls_integer;
    v_affected_rows pls_integer;
BEGIN
    v_cursor_id := DBMS_SQL.OPEN_CURSOR;
    DBMS_SQL.PARSE(v_cursor_id, 'update '||v_table_name||' set salary=salary+500 WHERE job_id = :jid',DBMS_SQL.NATIVE);
    DBMS_SQL.BIND_VARIABLE(v_cursor_id, ':jid','IT_PROG');
    v_affected_rows := DBMS_SQL.EXECUTE(v_cursor_id);
    dbms_output.put_line(v_affected_rows|| ' rows are updated by dbms_sql!');
    DBMS_SQL.CLOSE_CURSOR(v_cursor_id);
END;
/

DECLARE
    v_table_name varchar2(20) := 'employees_copy';
    v_cursor_id pls_integer;
    v_affected_rows pls_integer;
BEGIN
    v_cursor_id := DBMS_SQL.OPEN_CURSOR;
    DBMS_SQL.PARSE(v_cursor_id, 'update '||v_table_name||' set salary=salary+:inc WHERE job_id = :jid',DBMS_SQL.NATIVE);
    DBMS_SQL.BIND_VARIABLE(v_cursor_id, ':jid','IT_PROG');
    DBMS_SQL.BIND_VARIABLE(v_cursor_id, ':inc','5');
    v_affected_rows := DBMS_SQL.EXECUTE(v_cursor_id);
    dbms_output.put_line(v_affected_rows|| ' rows are updated by dbms_sql!');
    DBMS_SQL.CLOSE_CURSOR(v_cursor_id);
END;
/

SELECT * FROM user_tab_columns;
EXEC prc_method4_example('employees');
EXEC prc_method4_example('departments');
EXEC prc_method4_example('countries');
EXEC prc_method4_example('locations');
/

create or replace PROCEDURE prc_method4_example (p_table_name IN VARCHAR2) IS
    TYPE t_columns IS TABLE OF user_tab_columns%rowtype INDEX BY PLS_INTEGER;

    v_columns               t_columns;
    v_columns_with_commas   VARCHAR2(32767);
    v_number_value          NUMBER;
    v_string_value          VARCHAR2(32767);
    v_date_value            DATE;
    v_output_string         VARCHAR2(32767);
    cur_dynamic             INTEGER;

BEGIN
    SELECT * BULK COLLECT INTO v_columns FROM user_tab_columns WHERE table_name = upper(p_table_name);

    v_columns_with_commas:=v_columns(1).column_name;

    FOR i IN 2..v_columns.COUNT LOOP
        v_columns_with_commas:=v_columns_with_commas||','||v_columns(i).column_name;
    END LOOP;

    cur_dynamic := dbms_sql.open_cursor;

    dbms_sql.parse(cur_dynamic,'SELECT '||v_columns_with_commas||' FROM '||p_table_name,dbms_sql.NATIVE);

     FOR idx IN 1..v_columns.COUNT LOOP
        IF v_columns(idx).data_type = 'NUMBER' THEN
            dbms_sql.define_column(cur_dynamic,idx,1);
        ELSIF v_columns(idx).data_type IN ('VARCHAR2','VARCHAR','CHAR') THEN
            dbms_sql.define_column(cur_dynamic,idx,'dummy text',v_columns(idx).char_length);

        ELSIF v_columns(idx).data_type = 'DATE' THEN
            dbms_sql.define_column(cur_dynamic,idx,sysdate);
        END IF;

        v_output_string:=v_output_string||'  '||rpad(v_columns(idx).column_name,20);

     END LOOP;

     dbms_output.put_line(v_output_string);

     v_number_value:=dbms_sql.execute(cur_dynamic);

     WHILE dbms_sql.fetch_rows(cur_dynamic) > 0 LOOP

        v_output_string:=NULL;

        FOR t IN 1..v_columns.COUNT LOOP
            IF v_columns(T).data_type = 'NUMBER' THEN
                dbms_sql.column_value(cur_dynamic,t,v_number_value);
                v_output_string := v_output_string||'  '||rpad(nvl(to_char(v_number_value),' '),20);
            ELSIF v_columns(T).data_type IN ('VARCHAR2','VARCHAR','CHAR') THEN
                dbms_sql.column_value(cur_dynamic,t,v_string_value);
                v_output_string := v_output_string||'  '||rpad(nvl(to_char(v_string_value),' '),20);
            ELSIF v_columns(T).data_type = 'DATE' THEN
                dbms_sql.column_value(cur_dynamic,t,v_date_value);
                v_output_string := v_output_string||'  '||rpad(nvl(to_char(v_date_value),' '),20);
            END IF;
        END LOOP;
        dbms_output.put_line(v_output_string);
     END LOOP;
END;
```

### Using Oracle-Supplied Packages

Using the DBMS_OUTPUT Package

- provides debugging and buffering of text data
- `PUT(item IN varchar2)`: appends text to the current line of the buffer
- `PUT_LINE(item IN varchar2)`: same as `PUT()` with an additional end-of-line character, maximum 32767 bytes of data
- `NEW_LINE`: adds an end-of-line(new line) character
- `GET_LINE(line OUT varchar2, status OUT INTEGER)`: gets the current line of text from the buffer starting from the first line
- `GET_LINES(lines OUT CHARARR, num_of_lines IN OUT INTEGER)`: gets the current line of text from the buffer starting from the first line
- `DISABLE`: disable the calls to PUT, PUT_LINE, NEW_LINE, GET_LINE, and GET_LINES procedures
- `ENABLE(buffer IN INTEGER DEFAULT 20000)`: enables the package for operations and assigns a buffer with the given size
  - default: 20000 bytes, pass in `NULL` means no limit

```sql
EXEC dbms_output.put('Test No:3'); -- nothing print, because it still in the buffer
/
EXEC dbms_output.put_line('Test No:4');
/

CREATE TABLE temp_table(ID NUMBER GENERATED ALWAYS AS IDENTITY, text VARCHAR2(1000));

/
EXEC dbms_output.enable;
EXEC dbms_output.put_line('Hi');
/

DECLARE
    v_buffer VARCHAR2(1000);
    v_status INTEGER;
BEGIN
    dbms_output.put('...');
    dbms_output.put_line('Hello');
    dbms_output.put_line('How are you');
    FOR I IN 1..10 LOOP
        dbms_output.get_line(v_buffer,v_status);
        IF v_status = 0 THEN
            INSERT INTO temp_table(text) VALUES (v_buffer);
            -- output: 1. Hi 2. ...Hello 3. How are you
        END IF;
    END LOOP;
END;
/

SELECT * FROM temp_table;
/
SET SERVEROUTPUT ON;
DECLARE
    v_buffer VARCHAR2(1000);
    v_status INTEGER;
BEGIN
    dbms_output.put('...');
    dbms_output.put_line('Hello');
    dbms_output.put_line('How are you');
    dbms_output.get_line(v_buffer,v_status);
    -- output: How are you
END;
/
SET SERVEROUTPUT OFF;
EXEC dbms_output.enable;
/
DECLARE
    v_buffer dbms_output.chararr; -- the get_lines procedure needs to take in an array as the first parameter
    v_num_lines INTEGER:= 30;
BEGIN
    dbms_output.put('...');
    dbms_output.put_line('Hello');
    dbms_output.put_line('How are you');
    dbms_output.get_lines(v_buffer,v_num_lines);
    FOR i IN 1..v_num_lines LOOP
        INSERT INTO temp_table(text) VALUES (v_buffer(I));
        -- output: 1. ...Hello 2. How are you
    END LOOP;
END;
/
DROP TABLE temp_table;
```

Using the UTL_FILE Package

- the UTL_FILE operates on:

  - the paths(directories) specified in the UTL_FILE_DIR parameter
    `UTL_FILE_DIR = directory`, wins `UTL_FILE_DIR = C:\My_files`, Linux `UTL_FILE_DIR = /users/test_area`
  - database directory aliases created for specific paths(directories)
    - `create or replace directory test_dir as 'C:\my_files';`
    - the database does not check the path for existence
    - directory names must be written in uppercase when using the UTL_FILE subprograms
    - the existing directories: `DBA_DIRECTORIES` `ALL_DIRECTORIES`

- The UTL_FILE subprograms:

  - `FOPEN` Function: opens a file to read, write or append, with maximum 50 files

    ```sql
        FOPEN ( Location in varchar2, -- with upper case, and case-sensitive
                file_name in varchar2,
                open_mode in varchar2, -- 'r' 'w' 'a'
                max_linesize in binary_integer default null)
        return file_type;

        type file_type is record( id binary_integer,
                                  data_type binary_integer,
                                  byte_mode boolean);
    ```

  - `IS_OPEN` Function: returns true if the file is already open
    ```sql
        IS_OPEN (file in FILE_TYPE) return boolean;
    ```
  - `FCLOSE` Procedure: closes an open file with the given file handle
    ```sql
        FCLOSE (file IN OUT FILE_TYPE);
    ```
  - `FCLOSE_ALL`: closes all opened files
  - `GET_LINE` Procedure: reads text from an open file
    ```sql
        get_line(file in file_type,
                 buffer out varchar2,
                 len in binary_integer default null);
    ```
  - `PUT` Procedure: writes a text to the file without a newline character
    ```sql
        put(file in file_type,
            buffer in varchar2);
    ```
  - `NEW_LINE` Procedure: writes one or more line terminators to the file
    ```sql
        new_line(file in file_type,
                 lines in binary_integer := 1);
    ```
  - `PUT_LINE` Procedure: writes a text to the file with a newline character
    ```sql
        put_line(file in file_type,
                 buffer in varchar2,
                 autoflush in boolean default false);
    ```
  - `FFLUSH` Procedure: physically writes the buffered data to disks
    ```sql
        FFLUSH (file in file_type);
    ```
  - `PUTF` Procedure: provides formatted put procedure
    ```sql
        PUTF (file in file_type,
              format in varchar2,
              argument in varchar2 default null);
    ```
  - `FCOPY` Procedure: copies a portion or the complete file to a newly create file
    ```sql
        FCOPY ( scr_location in varchar2,
                scr_filename in varchar2,
                dest_location in varchar2,
                dest_filename in varchar2,
                start_line in binary_integer default 1,
                end_line in binary_integer default null);
    ```
  - `FRENAME` Procedure: rename and move a file to another destination
    ```sql
        FRENAME ( scr_location in varchar2,
                scr_filename in varchar2,
                dest_location in varchar2,
                dest_filename in varchar2,
                overwrite in boolean default false);
    ```
  - `FREMOVE` Procedure: removes the file from the disk
    ```sql
        FREMOVE ( scr_location in varchar2,
                  scr_filename in varchar2);
    ```
  - `FGETATTR` Procedure: retrieves the existence, length and file-system block size information
    ```sql
        FGETATTR ( location in varchar2,
                   filename in varchar2,
                   fexists out boolean,
                   file_length out number,
                   block_size out binary_integer);
    ```

```sql
--------------------CREATE DIRECTORY------------------------------------
CREATE DIRECTORY test_dir AS 'C:\My Folder';
/

-------------------GET ALL THE EXISTING DIRECTORIES--------------------
SELECT * FROM all_directories;
/

-------------------READ FROM A FILE------------------------------------
SET SERVEROUTPUT ON;
DECLARE
    v_file UTL_FILE.FILE_TYPE;
    v_line VARCHAR2(32767); -- maximum of line size
BEGIN
    v_file := UTL_FILE.FOPEN('TEST_DIR', 'temp file.txt', 'R', 32767);
    LOOP
        UTL_FILE.GET_LINE(v_file, v_line);
        dbms_output.put_line (v_line);
    END LOOP;
    EXCEPTION
        WHEN no_data_found THEN
            dbms_output.put_line('The whole file is read!');
            UTL_FILE.FCLOSE(v_file);
END;
/

-------------------GRANT OR REVOKE READ-WRITE PRIVILEGES---------------
GRANT READ, WRITE ON DIRECTORY test_dir TO hr;
REVOKE READ, WRITE ON DIRECTORY test_dir FROM hr;
/

-------------------WRITE TO A FILE USING PUT_LINE PROCEDURE-------------
DECLARE
    v_file UTL_FILE.FILE_TYPE;
BEGIN
    v_file := UTL_FILE.FOPEN('TEST_DIR', 'temp file.txt', 'w', 32767);
    FOR r_emp IN (select * from employees) LOOP
        UTL_FILE.PUT_LINE(v_file, r_emp.first_name||' '||r_emp.last_name);
    END LOOP;
    UTL_FILE.FCLOSE(v_file);
END;
/
-------------------WRITE TO A FILE USING PUT AND NEW_LINE---------------
DECLARE
    v_file UTL_FILE.FILE_TYPE;
BEGIN
    v_file := UTL_FILE.FOPEN('TEST_DIR', 'temp file.txt', 'w', 32767);
    FOR r_emp IN (select * from employees) LOOP
        UTL_FILE.PUT(v_file, r_emp.first_name||' '||r_emp.last_name);
        UTL_FILE.NEW_LINE(v_file);
    END LOOP;
    UTL_FILE.FCLOSE(v_file);
END;
/

-------------------WRITE TO A FILE USING PUTF---------------------------
DECLARE
    v_file UTL_FILE.FILE_TYPE;
BEGIN
    v_file := UTL_FILE.FOPEN('TEST_DIR', 'temp file.txt', 'w', 32767);
    FOR r_emp IN (select * from employees) LOOP
        UTL_FILE.PUTF(v_file, '--> %s %s',r_emp.first_name,r_emp.last_name);
        UTL_FILE.NEW_LINE(v_file);
        --UTL_FILE.PUTF(v_file, '--> %s %s\n',r_emp.first_name,r_emp.last_name);
    END LOOP;
    UTL_FILE.FCLOSE(v_file);
END;
/

-------------------USING FFLUSH TO WRITE IMMEDIATELY-------------------
DECLARE
    v_file UTL_FILE.FILE_TYPE;
BEGIN
    v_file := UTL_FILE.FOPEN('TEST_DIR', 'temp file.txt', 'w', 32767);
    FOR r_emp IN (select * from employees) LOOP

        UTL_FILE.PUT_LINE(v_file,r_emp.first_name||' '||r_emp.last_name);
        UTL_FILE.FFLUSH(v_file);
        -- alternative >> add 'true' into parameter
        --UTL_FILE.PUT_LINE(v_file,r_emp.first_name||' '||r_emp.last_name,true);
        DBMS_SESSION.SLEEP(1);
    END LOOP;
    UTL_FILE.FCLOSE(v_file);
END;
/

-------------------CHECK FILE ATTRIBUTES-----------------------------
DECLARE
    v_fexists       BOOLEAN;
    v_file_length   NUMBER;
    v_block_size    BINARY_INTEGER;
BEGIN
    UTL_FILE.FGETATTR('TEST_DIR','temp file.txt',v_fexists,v_file_length,v_block_size);
    IF v_fexists THEN
        DBMS_OUTPUT.PUT_LINE('The file exists');
        DBMS_OUTPUT.PUT_LINE('Its length is     :'||v_file_length);
        DBMS_OUTPUT.PUT_LINE('Its block size is :'||v_block_size);
    ELSE
        DBMS_OUTPUT.PUT_LINE('The file does not exist!');
    END IF;
END;
/

-------------------COPY THE FILE---------------------------------------
EXECUTE UTL_FILE.FCOPY('TEST_DIR','temp file.txt','TEST_DIR','temp file copy.txt');
/

-- copy file with certain lines
-------------------COPY THE FILE EX2-----------------------------------
EXECUTE UTL_FILE.FCOPY('TEST_DIR','temp file.txt','TEST_DIR','temp file copy2.txt',1,5);
/

-------------------RENAME THE FILE-------------------------------------
EXECUTE UTL_FILE.FRENAME('TEST_DIR','temp file copy2.txt','TEST_DIR','temp file renamed.txt');
/

-------------------REMOVE THE FILE-------------------------------------
EXECUTE UTL_FILE.FREMOVE('TEST_DIR','temp file renamed.txt');
EXECUTE UTL_FILE.FREMOVE('TEST_DIR','temp file copy.txt');
EXECUTE UTL_FILE.FREMOVE('TEST_DIR','temp file.txt');
/
-------------------DROP THE DIRECTORY-----------------------------------
DROP DIRECTORY test_dir;
```

Using the UTL_MAIL Package

- `SEND` Procedure: sends messages to the related SMTP server for forwarding to the recipients

```sql
    send(
        sender     in varchar2 character set any_cs ,
        recipients in varchar2 character set any_cs ,
        cc         in varchar2 character set any_cs default null,
        bcc        in varchar2 character set any_cs default null,
        subject    in varchar2 character set any_cs default null,
        message    in varchar2 character set any_cs default null,
        mime_type  in varchar2 character set any_cs default 'text/plain;charset=us-ascii',
        priority   in pls_integer default 3,
        replyto    in varchar2 character set any_cs default null
    );
```

- `SEND_ATTACH_RAW` Procedure: sends emails with a binary attachment

```sql
    send_attach_raw(
        sender     in varchar2 character set any_cs ,
        recipients in varchar2 character set any_cs ,
        cc         in varchar2 character set any_cs default null,
        bcc        in varchar2 character set any_cs default null,
        subject    in varchar2 character set any_cs default null,
        message    in varchar2 character set any_cs default null,
        mime_type  in varchar2 character set any_cs default 'text/plain;charset=us-ascii',
        priority   in pls_integer default 3,
        attachment in RAW,
        att_inline in boolean default true,
        att_mine_type in varchar2 character set any_cs default 'application/octet',
        att_filename in varchar2 character set any_cs default null,
        replyto    in varchar2 character set any_cs default null
    );
```

```sql
--Sending an email with the least number of parameters
BEGIN
    UTL_MAIL.send(
                  sender     => 'somebody@somedomain.com',
                  recipients => 'oraclemaster@outlook.com',
                  subject    => 'Example 1: Test Email Subject',
                  message    => 'This is a test email from someone.'
                 );
END;
/

--Sending an email with specific names to the sender and recipients
BEGIN
    UTL_MAIL.send(
                  sender     => 'Some Person <somebody@somedomain.com>',
                  recipients => 'Oracle Masters <oraclemaster@outlook.com>',
                  subject    => 'Example 2: Test Email Subject',
                  message    => 'This is a test email from someone.'
                 );
END;
/

--Sending an email with using all of the parameters
BEGIN
    UTL_MAIL.send(
                  sender     => 'somebody@somedomain.com',
                  recipients => 'oraclemaster@outlook.com',
                  cc         => 'somemanager@somedomain.something,someotherperson@somedomain.something',
                  bcc        => 'someothermanager@somedomain.com',
                  subject    => 'Example 3: Test Email Subject',
                  message    => 'This is a test email from someone.',
                  mime_type  => 'text/plain; charset=us-ascii',
                  priority   => 1,
                  replyto    => 'somereplyaddress@somedomain.com'
                 );
END;
/

--Sending an email by dynamically filling the message body
DECLARE
    cursor cur_top_earning_emps is
                    select employee_id, first_name, last_name, salary
                    from hr.employees
                    where salary > 10000
                    order by salary desc;
    v_message varchar2(32767);
BEGIN
    v_message := 'EMPLOYEE ID'||CHR(9)||'FIRST NAME'||CHR(9)||'LAST NAME'||CHR(9)||'EMPLOYEE ID'||CHR(13);
    for r_top in cur_top_earning_emps loop
        v_message := v_message||r_top.employee_id||CHR(9)||r_top.first_name||CHR(9)||r_top.last_name||CHR(9)||r_top.salary||CHR(13);
    end loop;

    UTL_MAIL.send(
                  sender     => 'topearnings@somedns.com',
                  recipients => 'oraclemaster@outlook.com',
                  subject    => 'Example 4: The Employees Earning More Than $10000',
                  message    => v_message
                 );
END;
/

--Sending an HTTP mail
DECLARE
    cursor cur_top_earning_emps is
                    select employee_id, first_name, last_name, salary
                    from hr.employees
                    where salary > 10000
                    order by salary desc;
    v_message varchar2(32767);
BEGIN
    v_message := '<!DOCTYPE html>
                    <html>
                       <head>
                          <meta charset=''Cp1252''>
                          <title>Top Earning Employees</title>
                          <meta name="viewport" content="width=device-width, initial-scale=1.0">
                          <style>
                             * {
                             margin: 0;
                             padding: 0;
                             }
                             body {
                             font: 14px/1.4 Georgia, Serif;
                             }
                             /*
                             Generic Styling, for Desktops/Laptops
                             */
                             table {
                             width: 100%;
                             border-collapse: collapse;
                             }
                             /* Zebra striping */
                             tr:nth-of-type(odd) {
                             background: #eee;
                             }
                             th {
                             background: #333;
                             color: white;
                             font-weight: bold;
                             }
                             td, th {
                             padding: 6px;
                             border: 1px solid #9B9B9B;
                             text-align: left;
                             }
                             @media
                             only screen and (max-width: 760px),
                             (min-device-width: 768px) and (max-device-width: 1024px)  {
                             table, thead, tbody, th, td, tr { display: block; }
                             thead tr { position: absolute;top: -9999px;left: -9999px;}
                             tr { border: 1px solid #9B9B9B; }
                             td { border: none;border-bottom: 1px solid #9B9B9B; position: relative;padding-left: 50%; }
                             td:before { position: absolute;top: 6px;left: 6px;width: 45%; padding-right: 10px; white-space: nowrap;}
                             /*
                             Label the data
                             */
                             td:nth-of-type(0):before { content: "EMPLOYEE_ID"; }
                             td:nth-of-type(1):before { content: "FIRST_NAME"; }
                             td:nth-of-type(2):before { content: "LAST_NAME"; }
                             td:nth-of-type(3):before { content: "SALARY"; }
                             }
                             }
                          </style>
                          <!--<![endif]-->
                       </head>
                       <body>
                          <h1 style = ''text-align :center; color:green;''>Employees Earning more than $10.000 Per/month</h1>
                          <br>
                          <table>
                             <thead>
                                <tr>
                                   <th>EMPLOYEE_ID</th>
                                   <th>FIRST_NAME</th>
                                   <th>LAST_NAME</th>
                                   <th>SALARY</th>
                                </tr>
                             </thead>
                             <tbody id="data">';

    for r_top in cur_top_earning_emps loop
        v_message := v_message|| '<tr>'||
                                     '<td align="right">'||r_top.employee_id||'</td>'||
                                     '<td>'||r_top.first_name||'</td>'||
                                     '<td>'||r_top.last_name||'</td>'||
                                     '<td align="right">'||r_top.salary||'</td>'||
                                 '</tr>';

    end loop;
    v_message := v_message||'           </tbody>
                                      </table>
                                   </body>
                                </html>';
    UTL_MAIL.send(
                  sender     => 'topearnings@somedns.com',
                  recipients => 'oraclemaster@outlook.com',
                  subject    => 'Example 5: The Employees Earning More Than $10000 (HTML Formatted)',
                  message    => v_message,
                  mime_type  => 'text/html'
                 );
END;
/

------------------SEND ATTACH RAW------------
--Create a temp table
CREATE TABLE temp_table(
  id        NUMBER,
  blob_data BLOB
);
/

--2) Create a directory object
CREATE OR REPLACE DIRECTORY BLOB_DIR AS 'C:\blob_directory\';
/

--3)Write a PL/SQL Block to load your external file into a BLOB/CLOB column.
DECLARE
  v_bfile       BFILE;
  v_blob        BLOB;
  v_dest_offset INTEGER := 1;
  v_src_offset  INTEGER := 1;
BEGIN
  INSERT INTO temp_table (id, blob_data)
      VALUES (222,  empty_blob())
          RETURNING blob_data INTO v_blob;

  v_bfile := BFILENAME('BLOB_DIR', 'test_file.jpeg');
  DBMS_LOB.fileopen(v_bfile, DBMS_LOB.file_readonly);
  DBMS_LOB.loadblobfromfile (
             dest_lob    => v_blob,              -- Destination lob
             src_bfile   => v_bfile,             -- Source file path and name in the OS
             amount      => DBMS_LOB.lobmaxsize, -- Maximum LOB size.
             dest_offset => v_dest_offset,       -- Destination offset.
             src_offset  => v_src_offset);       -- Source offset.
  DBMS_LOB.fileclose(v_bfile);
  COMMIT;
END;
/

--4) Check the table to see if we could insert the blob file or not
SELECT * FROM temp_table;
/

--5) Send email with an attachment
DECLARE
    v_file BLOB;
    v_rawbuf RAW(32767);
BEGIN
    select blob_data into v_file from temp_table where rownum = 1;
    v_rawbuf := dbms_lob.substr(v_file);
    UTL_MAIL.send_attach_raw
    (
        sender => 'somebody@somedomain.com',
        recipients => 'oraclemaster@outlook.com',
        subject => 'Example 6: Attachment Test',
        message => 'This is a raw data',
        attachment => v_rawbuf,
        att_inline => TRUE,
        att_filename => 'testImage.jpeg'
    );
END;
/

DROP DIRECTORY blob_dir;
DROP TABLE temp_table;
/

--5) Send email with a text attachment
BEGIN
    UTL_MAIL.send_attach_varchar2
    (
        sender => 'somebody@somedomain.com',
        recipients => 'oraclemaster@outlook.com',
        subject => 'Example 7: Text Attachment Test',
        message => 'This is a text data',
        attachment => 'This is the text that will be written inside of the text file.',
        att_inline => TRUE,
        att_filename => 'testTextFile.txt'
    );
END;
```
